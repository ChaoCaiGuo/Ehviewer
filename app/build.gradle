apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'android-chinese-string'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.hippo.ehviewer"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 88
        versionName "1.3.1"
        vectorDrawables.useSupportLibrary = true
        resConfigs "zh", "zh-rCN", "zh-rHK", "zh-rTW",
                "es", "ja", "ko", "fr", "de"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError true
        checkReleaseBuilds true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"com.hippo.ehviewer.fileprovider"'
        }
        debug {
            applicationIdSuffix ".debug"
            buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"com.hippo.ehviewer.debug.fileprovider"'
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
        }
    }
}

task copyNotice(type: Copy) {
    from '../NOTICE.html'
    into './src/main/assets'
    finalizedBy ":daogenerator:executeDaoGenerator"
}

tasks.withType(JavaCompile) {
    task -> task.dependsOn copyNotice
}

clean {
    delete file('src/main/assets/NOTICE.html').absolutePath
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportLibrary"
    implementation "com.android.support:cardview-v7:$supportLibrary"
    implementation "com.android.support:design:$supportLibrary"
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'com.github.seven332:android-recaptcha:2bbb3459a8'
    implementation 'com.github.seven332:android-resource:0.1.0'
    implementation 'com.github.seven332:animator:0.1.0'
    implementation 'com.github.seven332:beerbelly:0.1.4'
    implementation 'com.github.seven332.commons-compress:android-lite:464e96d3b4'
    implementation 'com.github.seven332:conaco:0.1.5-eh'
    implementation 'com.github.seven332:drawerlayout:0.2.1'
    implementation 'com.github.seven332:easyrecyclerview:0.1.1'
    implementation 'com.github.seven332:glgallery:0.1.2'
    implementation 'com.github.seven332:glview:0.1.0'
    implementation 'com.github.seven332:glview-image:0.1.0'
    implementation 'com.github.seven332:hotspot:0.1.0'
    implementation 'com.github.seven332:image:0.1.12'
    implementation 'com.github.seven332.junrar:android-lite:46ecc9630a'
    implementation 'com.github.seven332:okhttp:3.5.0'
    implementation 'com.github.seven332:refreshlayout:0.1.0'
    implementation 'com.github.seven332:ripple:0.1.2'
    implementation 'com.github.seven332:streampipe:0.1.0'
    implementation 'com.github.seven332:tuxiang:0.1.2'
    implementation 'com.github.seven332:unifile:a3571536be'
    implementation 'com.github.seven332:yorozuya:0.1.2'
    implementation 'com.github.seven332:yorozuya-thread:0.1.1'
    implementation 'com.github.seven332:yorozuya-collect:0.1.4'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.11.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'org.greenrobot:greendao:2.2.1'
    implementation 'org.jsoup:jsoup:1.11.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.jooq:joor:0.9.6'
}

configurations.all {
    resolutionStrategy {
        force 'com.github.seven332:glgallery:c56b14c6fa'
        force 'com.github.seven332:glview-image:4c744db'
        force 'com.github.seven332:image:29266a3c7b'

        exclude group: 'com.github.seven332', module: 'okhttp'
        exclude group: 'com.squareup.okio', module: 'okio'
    }
}
